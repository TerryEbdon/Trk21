package net.ebdon.trk21;

/**
 * @file
 * @author      Terry Ebdon
 * @date        January 2019
 * @copyright   Terry Ebdon, 2019
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

@groovy.util.logging.Log4j2
final class Quadrant extends GameSpace {

  enum Thing {
    emptySpace( '.' ),
    ship(       'E' ),
    enemy(      'K' ),
    base(       'B' ),
    star(       '*' ),
    torpedo(    't' )

    private final List<Integer> multipliers = [0, 0, 100, 10, 1, 0]
    final String symbol;
    Thing( final String sym ) { symbol = sym }
    int getMultiplier() { multipliers[ordinal()] }
  }

  int getCellPadding() {
    1
  }

  String symbol( final List<Integer> key ) {
    board.get( key ).symbol
  }

  @groovy.transform.TypeChecked
  boolean isOccupied( final int i, final int j ) {
    board[i,j] != Thing.emptySpace
  }

  // @groovy.transform.TypeChecked
  final Thing getAt( final Coords2d c2d ) {
    board.get c2d.toList()
  }

  // @groovy.transform.TypeChecked
  final Thing getAt( List<Integer> key ) {
    board.get key
  }

  final putAt( key, Thing value ) {
    board[key] = value
  }

  final putAt( final Coords2d coords, Thing value ) {
    board[coords.row,coords.col] = value
  }

  @groovy.transform.TypeChecked
  final void clearSquare( final Coords2d squareCoords ) {
    board[squareCoords.toList()] = Thing.emptySpace
  }

  /// @bug Infinite loop is possible.
  /// @todo add a time-out.
  List<Integer> getEmptySector() {
    int r1
    int r2
    (r1,r2) = getRandomCoords()
    log.trace "getEmptySector() starts with r1:$r1, r2:$r2 = ${board[r1,r2]} board size is ${board.size()}"
    while ( isOccupied(r1,r2) ) {
       (r1,r2) = randomCoords
       log.trace "Is ${[r1,r2]} empty?"
    }
    log.trace "getEmptySector() ends with r1:$r1, r2:$r2 = ${board[r1,r2]}"
    [r1,r2]
  }

  /**
    Output the sectors of the current quadrant, showing the position of all game
    objects present. This is the top-half of the display generated by
    Trek.shortRangeScan(). It's also used for logging in Trek.positionEnemy()
    during the sector setup.

    @arg linePrinter - A closure that will be called to output each line of the
    sector display.
  */
  @groovy.transform.TypeChecked
  void displayOn( Closure linePrinter ) {
    eachCoord { int row ->
      String scanLine = ''
      eachCoord { int col ->
        scanLine += "${symbol([row, col])} "
      }
      linePrinter( scanLine )
    }
  }

  List<Coords2d> findEnemies() {
    board.findAll { Map.Entry candidate ->
      candidate.value == Thing.enemy
    }?.collect { Map.Entry foundEntry ->
      new Coords2d( row: foundEntry.key.first(), col: foundEntry.key.last() )
    }
  }

  @groovy.transform.TypeChecked
  void removeEnemy( final Coords2d key ) {
    assert this[key] == Thing.enemy
    clearSquare key
  }

  @groovy.transform.TypeChecked
  int count( Closure closure ) {
    board.count( closure ).toInteger()
  }
}
